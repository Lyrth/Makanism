
apply from: 'dependencies.gradle'
apply from: 'extern.gradle'

ext {
    jdk = '1.11'
    
    absoluteOutputDir = "$rootDir/$outputDir" as String
    absoluteOutputLibDir = "$absoluteOutputDir/$outputLibDir" as String
}

allprojects {
    apply plugin: 'java'

    sourceCompatibility = jdk
    targetCompatibility = jdk

    group = artifact_group
    version = project_version
}

subprojects {
    archivesBaseName = "makanism-$project.name"

    jar {
        manifest {
            attributes(
                    'Implementation-Title': archivesBaseName,
                    'Implementation-Version': archiveVersion
            )
        }
    }
}

dependencies {
    implementation project(':api')
    implementation project(':common')
    runtimeOnly project(':bot')
}

jar.enabled = false     // disable jar creation for root project

task buildOutput {
    dependsOn(build)
    subprojects.each { pr -> dependsOn(":${pr.name}:jar") }
    shouldRunAfter(build)

    mkdir absoluteOutputDir
    mkdir absoluteOutputLibDir
    inputs .dir(absoluteOutputDir)
    outputs.dir(absoluteOutputDir)
    doLast {
        println "Copying dependencies into ${absoluteOutputLibDir}..."
        copy {  // copy the dependencies
            from configurations.runtimeClasspath
            into absoluteOutputLibDir
        }

        // Filter unneeded dependencies (from previous copies)
        Collection<String> inDir = layout.files {
            file(absoluteOutputLibDir).listFiles()
        }.collect { it.name }

        Collection<String> required = configurations.runtimeClasspath.collect { it.name }

        Collection<String> toDelete = (inDir - required).collect { "$absoluteOutputLibDir/$it" as String }

        println 'Deleting unneeded files...'
        toDelete.each { println "- $it" }
        delete toDelete

        String mainJarName = bot_archive_name
        if (!mainJarName){
            println("jarName is blank!")
            return
        }
        String jarFile = "$absoluteOutputLibDir/$mainJarName"

        println 'Moving main jar file...'
        if (!file(jarFile).exists()) return
        copy {
            from jarFile
            into absoluteOutputDir
        }
        delete jarFile
    }
}
